plugins {
    id 'org.liquibase.gradle' version '2.0.4'
    id 'java'
    id 'war'
}

group = 'com.ilyastuit'
version = '1.0'
targetCompatibility = 11
sourceCompatibility = 11

jar {
    enabled = false
}

war {
    getArchiveFileName().set("backend-tomcat.war")
}

configurations {
    unitTestImplementation.extendsFrom(testImplementation)
    unitTestRuntimeOnly.extendsFrom(testRuntimeOnly)

    integrationTestImplementation.extendsFrom(testImplementation)
    integrationTestRuntimeOnly.extendsFrom(testRuntimeOnly)
}

dependencies {
    // Spring
    implementation("org.springframework:spring-webmvc:${parent.ext.springVersion}")
    implementation("org.springframework:spring-aspects:${parent.ext.springVersion}")
    implementation("org.springframework:spring-orm:${parent.ext.springVersion}")
    implementation("org.springframework.kafka:spring-kafka:2.8.6")

    // Javax (Jakarta) API
    compileOnly("javax.servlet:javax.servlet-api:${parent.ext.servletVersion}")

    // Javax (Jakarta) Implementation
    implementation("org.hibernate.validator:hibernate-validator:${parent.ext.hibernateValidatorVersion}")

    // Http Converters
    implementation("com.fasterxml.jackson.core:jackson-databind:${parent.ext.jacksonVersion}")

    // Logging
    implementation("ch.qos.logback:logback-classic:${parent.ext.logbackVersion}")

    // Persistence
    implementation("org.hibernate:hibernate-entitymanager:${parent.ext.hibernateVersion}")
    implementation("org.hibernate:hibernate-hikaricp:${parent.ext.hibernateVersion}")
    implementation("com.zaxxer:HikariCP:${parent.ext.hikariCPVersion}")

    // Other
    implementation("at.favre.lib:bcrypt:${parent.ext.bcryptLibraryVersion}")
    implementation("org.glassfish.expressly:expressly:${parent.ext.expresslyVersion}")

    // Database Migration
    liquibaseRuntime("org.liquibase:liquibase-core:${parent.ext.liquibaseVersion}")
    liquibaseRuntime("org.postgresql:postgresql:${parent.ext.postgresVersion}")

    // Driver
    runtimeOnly("org.postgresql:postgresql:${parent.ext.postgresVersion}")

    // Unit Test
    unitTestImplementation("org.junit.jupiter:junit-jupiter-params:${parent.ext.jupiterParamsVersion}")
    unitTestImplementation("org.mockito:mockito-junit-jupiter:${parent.ext.mockitoJunitJupiterVersion}")
    unitTestRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${parent.ext.jupiterEngineVersion}")

    // Integration Test
    integrationTestImplementation("com.intuit.karate:karate-junit5:${parent.ext.karateJunit5Version}")
    integrationTestImplementation("com.intuit.karate:karate-apache:${parent.ext.karateApacheVersion}")
    integrationTestImplementation("org.springframework:spring-test:${parent.ext.springVersion}")
}

sourceSets {

    main {
        java.srcDirs('./src/main/java')
        resources.srcDirs('./src/main/resources')
    }

    unitTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('./src/unitTest/java')
        }
        resources.srcDir file('./src/unitTest/resources')
    }

    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output

            resources {
                srcDir file('./src/integrationTest/java')
                exclude '**/*.java'
            }
        }
        resources.srcDir file('./src/integrationTest/resources')
    }
}

tasks.withType(ProcessResources) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.withType(Test) {
    testLogging {
        events = ["passed", "failed", "skipped"]
        showStandardStreams = true
    }

    useJUnitPlatform()
}

task unitTest(type: Test) {
    description = 'Runs unit tests.'
    group = 'verification'

    testClassesDirs = sourceSets.unitTest.output.classesDirs
    classpath = sourceSets.unitTest.runtimeClasspath
}

task integrationTest(type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    systemProperty "karate.options", System.properties.getProperty("karate.options")
    systemProperty "karate.env", System.properties.getProperty("karate.env")

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    shouldRunAfter unitTest
}

liquibase {
    activities {
        main {
            File propsFile = new File("${rootDir}/${parent.ext.backendProjectDir}/src/main/resources/migrations/liquibase-${parent.ext.appEnv}.properties")
            Properties properties = new Properties()
            properties.load(new FileInputStream(propsFile))

            url properties['url']
            driver properties['driver']
            username properties['username']
            password properties['password']

            changeLogFile properties['changeLogFile']
        }
    }
}
